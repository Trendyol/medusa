apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    namespace 'com.trendyol.medusalib'
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

ext {
    PUBLISH_GROUP_ID = 'com.trendyol'
    PUBLISH_VERSION = '0.13.0'
    PUBLISH_ARTIFACT_ID = 'medusa'
    PUBLISH_DESCRIPTION = "Android Fragment Stack Controller"
    PUBLISH_URL = "https://github.com/Trendyol/medusa"
    PUBLISH_LICENSE_NAME = "Medusa License"
    PUBLISH_LICENSE_URL = "https://github.com/Trendyol/medusa/blob/master/LICENSE"
    PUBLISH_SCM_CONNECTION = "scm:git:github.com/Trendyol/medusa.git"
    PUBLISH_SCM_DEV_CONNECTION = "scm:git:ssh://github.com/Trendyol/medusa.git"
    PUBLISH_SCM_URL = "https://github.com/Trendyol/medusa/tree/main"
}


group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}

publishing {
    publications {
        release(MavenPublication) {
            groupId PUBLISH_GROUP_ID
            artifactId PUBLISH_ARTIFACT_ID
            version PUBLISH_VERSION

            afterEvaluate {
                from(components["release"])
            }

            pom {
                name = PUBLISH_ARTIFACT_ID
                description = PUBLISH_DESCRIPTION
                url = PUBLISH_URL

                licenses {
                    license {
                        name = PUBLISH_LICENSE_NAME
                        url = PUBLISH_LICENSE_URL
                    }
                }

                developers {
                    developer {
                        id = 'erkutaras'
                        name = 'Erkut Aras'
                        email = 'erkut.aras@trendyol.com'
                    }
                    developer {
                        id = 'muratcanbur'
                        name = 'Murat Can Bur'
                        email = 'muratcan.bur@trendyol.com'
                    }
                    developer {
                        id = 'bsobe'
                        name = 'Barış Söbe'
                        email = 'baris.sobe@trendyol.com'
                    }
                    developer {
                        id = 'MertNYuksel'
                        name = 'Mert Nevzat Yüksel'
                        email = 'mert.yuksel@trendyol.com'
                    }
                    developer {
                        id = 'ibrahimsn98'
                        name = 'İbrahim Süren'
                        email = 'ibrahim.suren@trendyol.com'
                    }
                    developer {
                        id = 'mucahidkambur'
                        name = 'Mücahid Kambur'
                        email = 'mucahid.kambur@trendyol.com'
                    }
                }

                scm {
                    connection = PUBLISH_SCM_CONNECTION
                    developerConnection = PUBLISH_SCM_DEV_CONNECTION
                    url = PUBLISH_SCM_URL
                }
            }
        }
    }
}


dependencies {
    implementation libs.androidXAppCompat
    implementation libs.androidXLifecycleCommon
    implementation libs.kotlinStdLib

    testImplementation libs.androidXFragmentTesting
    testImplementation libs.junit
    testImplementation libs.robolectric
    testImplementation libs.truth

    androidTestImplementation libs.androidXTestExtJunit
    androidTestImplementation libs.androidXTestRunner
    debugImplementation libs.androidXFragmentTesting
    androidTestImplementation libs.truth
    androidTestImplementation libs.junit
    androidTestImplementation libs.espressoTest

}

repositories {
    mavenCentral()
}
